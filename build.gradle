apply plugin: 'application'
mainClassName = 'com.mucommander.Launcher'

def javaSourceCompatibility = 1.5
def javaTargetCompatibility = 1.5

allprojects { project ->
    apply plugin: 'java'
	apply plugin: 'eclipse'
    group = 'org.mucommander'
    version = '0.9.0'
	
	sourceCompatibility = javaSourceCompatibility
	targetCompatibility = javaTargetCompatibility
	
	if (project.name != 'mucommander-commons-file')  {//NfsURLConnection uses non public API which would trigger restricted access error with default eclipse errors/warnings settings
		eclipse {
			classpath {
				file.whenMerged { classpath ->
					classpath.entries.removeAll { entry -> entry.kind == 'con' && entry.path =~ /^org.eclipse.jdt.launching.JRE_CONTAINER$/ }
				}
				containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/J2SE-' + javaSourceCompatibility
			}
		}
	}
	eclipse {
		jdt {
			sourceCompatibility = javaSourceCompatibility
			targetCompatibility = javaTargetCompatibility
			file.withProperties { 
				it['org.eclipse.jdt.core.compiler.compliance'] = javaTargetCompatibility.toString()
			}
		}
	}
}

repositories {
    jcenter()
    ivy {
        url = 'http://ivy.mucommander.com'
	layout "pattern", {
		ivy '[organisation]/[module]/[revision]/ivy.xml'
		artifact '[organisation]/[module]/[revision]/[artifact].[ext]'
	}
    }
}

dependencies {
    compile project('mucommander-commons-file')
    compile project('mucommander-commons-conf')
    compile project('mucommander-commons-collections')
    compile project('mucommander-commons-util')
    compile project('mucommander-commons-io')
    compile project('mucommander-commons-runtime')
    compile group: 'org.openjdk', name: 'java-extension', version: '6.0', configuration: 'runtime'

    compile 'jmdns:jmdns:3.1'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'ch.qos.logback:logback-core:1.1.2'
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'com.jidesoft:jide-oss:3.6.4'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    testCompile 'org.testng:testng:6.8.8'
    testCompile 'junit:junit:4.11'
}
